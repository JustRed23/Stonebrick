plugins {
    id 'java-library'
}

group 'dev.JustRed23'
version '1.9.1'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api 'org.jetbrains:annotations:24.1.0'
    api 'org.slf4j:slf4j-api:2.0.11'
    api 'org.json:json:20231013'

    api 'org.jooq:jooq:3.19.3'
    api 'org.jsoup:jsoup:1.17.2'

    api ('com.github.JustRed23:ABCM:2.0') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.reflections', module: 'reflections'
        exclude group: 'org.jetbrains', module: 'annotations'
        exclude group: 'org.json', module: 'json'
    }

    api ('com.zaxxer:HikariCP:5.1.0') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation ('org.reflections:reflections:0.10.2') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes('Implementation-Title': project.name, 'Implementation-Version': project.version)
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    archiveClassifier = 'all'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/**"
        exclude "module-info.class"
    }
    with jar
}
